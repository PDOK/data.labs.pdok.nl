prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix direct: <http://www.wikidata.org/prop/direct/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix geof: <http://www.opengis.net/def/function/geosparql/>
prefix uom: <http://www.opengis.net/def/uom/OGC/1.0/>
select ?wkt ?wktLabel {
  service <https://query.wikidata.org/sparql> {
    {
      ?municipalityWiki direct:P981 ?bagId ;
                        direct:P1082 ?population
    } optional {
      ?municipalityWiki direct:P41 ?municipalityFlag
    }
  }
  ?municipality geo:hasGeometry/geo:asWKT ?wkt ;
                bag:naamWoonplaats ?name ;
                bag:identificatiecode ?bagId
  bind(geof:envelope(?wkt) as ?rectangle)
  bind("^POLYGON\\s\\(\\(([0-9]*\\.[0-9]*)\\s([0-9]*\\.[0-9]*)\\,\\s([0-9]*\\.[0-9]*)\\s([0-9]*\\.[0-9]*)\\,\\s([0-9]*\\.[0-9]*)\\s([0-9]*\\.[0-9]*)\\,\\s([0-9]*\\.[0-9]*)\\s([0-9]*\\.[0-9]*).*\\)\\)$" as ?regex)
  bind(replace(str(?rectangle),?regex,"$1") as ?x1) bind(replace(str(?rectangle),?regex,"$2") as ?y1) bind(replace(str(?rectangle),?regex,"$5") as ?x2) bind(replace(str(?rectangle),?regex,"$4") as ?y2)
  bind(geof:distance(strdt(concat("Point (",str(?x1)," ",str(?y1),")"),geo:wktLiteral), strdt(concat("Point (",str(?x2)," ",str(?y1),")"),geo:wktLiteral), uom:metre) * 1e-3 as ?x)
  bind(geof:distance(strdt(concat("Point (",str(?x1)," ",str(?y1),")"),geo:wktLiteral), strdt(concat("Point (",str(?x1)," ",str(?y2),")"),geo:wktLiteral), uom:metre) * 1e-3 as ?y)
  bind(?x * ?y as ?area)
  bind (?population / ?area as ?density)
  bind (concat('<div><a href="',str(?municipality),'"><h3>',?name,'</h3></a></div><div style="display: flex;"><div><div>Population: ',str(?population),'</div><div>Area: ',str(xsd:integer(?area)),' kmÂ²</div><div>Density: ',str(xsd:integer(?density)),' ðŸ‘¤/kmÂ²</div></div>',if(bound(?municipalityFlag),concat('<div><img src="',str(?municipalityFlag),'" style="width: 60px;"></div>'),''),'</div>') as ?wktLabel)
}
order by desc(?density)
limit 25
