PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gsp: <http://www.opengis.net/ont/geosparql#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX schema: <http://schema.org/>
PREFIX bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX hg: <http://rdf.histograph.io/>
prefix bbi: <http://data.pdok.nl/def/bbi#>
PREFIX naa: <http://archief.nl/def/> 
PREFIX woz: <https://data.labs.pdok.nl/woz/def/>
SELECT 
*
FROM <http://archief.nl/NL-HaNA/doc/Graph:Verkaufsbucher>
FROM <http://archief.nl/verkaufsbucher/id/dataset/transactions>
FROM <http://data.pdok.nl/bbi/id/dataset/gemeente-woonplaats-relatie>
FROM <http://archief.nl/verkaufbuchen/id/dataset/linkset-wpl-bagwpl-exact>
FROM <http://archief.nl/verkaufsbucher/id/dataset/linkset-wpl-bagwpl-handmatig>
FROM <http://data.pdok.nl/id/dataset/bbi>
FROM <http://data.labs.pdok.nl/dataset/WOZ>
FROM <http://brk.basisregistraties.overheid.nl/id/dataset/brk/bestuurlijke-grenzen/2016>
FROM <http://archief.nl/verkaufsbucher/id/dataset/linkset-adres-bag-nummeraanduiding>
FROM <http://archief.nl/verkaufsbucher/id/dataset/verkaufsbucher-verkopersadressen>
{
  {
    select ?transactie2 ?kopers (group_concat(?verkoperlabel;separator=', ') as ?verkopers) 
    {
      {
        select ?transactie1 (group_concat(?koperlabel;separator=', ') as ?kopers) 
        {
          ?transactie1 
            a schema:TradeAction ;
            schema:object/vcard:hasAddress ?adres .
          ?adres 
            vcard:locality ?woonplaats .
          ?transactie1 schema:buyer ?koper .
          ?koper foaf:name | skos:prefLabel ?kopersnaam .
          filter (?kopersnaam in ("B.E.M. Hermans-Helsloot", "B.P.M. Hermans-Helsloot", "B.E.M. Hermans", "E.T.A. Hermans", "E.H.A. Hermans"))
#          filter (?kopersnaam in ("J.N. Brouwer"))
#          filter (str(?kopersnaam) in ("N.V. Bon Ami"))
#          filter (str(?kopersnaam) in ("T. Wolff"))
          optional {
            ?koper vcard:hasAddress/vcard:locality ?koperswpl .
          }
          bind(concat(?kopersnaam, ' (', (if (bound(?koperswpl), ?koperswpl, '?')),')') as ?koperlabel)
        }
        group by ?transactie1 ?adres
      }
      bind(if(bound(?transactie1), ?transactie1, bnode()) as ?transactie2)
      optional {
        ?transactie2 schema:seller ?verkoper .
        ?verkoper foaf:name ?verkopersnaam .
        optional {
          ?verkoper vcard:hasAddress/vcard:locality ?verkoperswpl .
        }
      }
      bind(concat(?verkopersnaam, ' (', (if (bound(?verkoperswpl), ?verkoperswpl, '?')),')') as ?verkoperlabel)
    }
    group by ?transactie2 ?kopers
  }
  bind(if(bound(?transactie2), ?transactie2, bnode()) as ?transactie)
  ?transactie schema:object/vcard:hasAddress ?adres .
  ?adres  
    vcard:street-address ?adreslabel ;
    foaf:depiction ?streetview ;
    owl:sameAs ?bag_num ;
    vcard:locality ?woonplaats ;
    naa:hasParcelCentroid/gsp:asWKT ?shape .
  ?adres hg:contains ?place .
  filter (?place != <http://archief.nl/NL-HaNA/id/land/Nederland>)
  optional {
    ?place owl:sameAs ?bag_wpl .
    ?bag_wpl gsp:sfWithin ?gem .
    ?gem rdfs:label ?gemeentenaam .
  }
  optional {
    service<https://data.pdok.nl/sparql> {
      ?bag_num bag:postcode ?postcode .
      graph ?vbog {
        ?bag_vbo bag:hoofdadres|bag:nevenadres ?bag_num ;
                               bag:oppervlakte ?opp ;
                               bag:pandrelatering ?bag_pnd .
      }
      #      ?vbog bag:beginGeldigheid ?vbo_begin .
      #      filter (?vbo_begin <= now())
      filter not exists {
        ?vbog bag:eindGeldigheid ?vbo_eind .
      }
      graph ?pndg {
        ?bag_pnd bag:oorspronkelijkBouwjaar ?bouwjaar .
        ?bag_pnd gsp:hasGeometry/gsp:asWKT ?bagShape .
      }
      #      ?pndg bag:beginGeldigheid ?pnd_begin .
      #      optional{
      #      	?pndg bag:eindGeldigheid ?pnd_eind .
      #      }
      filter not exists {
        ?pndg bag:eindGeldigheid ?pnd_eind .
      }
    }
  }
  optional {
    ?bag_num ^woz:aoaidentificatie / woz:waardeklasse / rdfs:label ?wozklasse .
  }
  bind(
    if (bound(?wozklasse), ?wozklasse, '?')
    as ?woz
  )
  optional {
    ?transactie naa:sellingPrice ?verkoopprijs .
  }
  optional {
    ?transactie naa:dateOfFinalSale ?definitieve_koopdatum .
  }
  bind(
    concat(
      '<h2>', ?adreslabel, '<br/>', ?woonplaats, '</h2>',
      '<hr/>',
      '<h3>Nationaal Archief gegevens:</h3>',
      '<ul>',
      '<li><b>verkoper(s):</b> ', ?verkopers, '</li>',
      '<li><b>koper(s):</b> ', ?kopers, '</li>',
      '<li><b>verkoopprijs:</b> &#402;', (if(bound(?verkoopprijs), str(?verkoopprijs), '?')), '</li>',
      '<li><b>definitieve koopdatum:</b> ', (if(bound(?definitieve_koopdatum), substr(str(?definitieve_koopdatum), 1, 10), '?')), '</li>',
      '<li><b>&euro; 2018 tov &#402; 1944:</b> &euro; ', (if(bound(?verkoopprijs), str(?verkoopprijs * 6.37), '?')), '</li>',
      '</ul>',
      '<h3>',if(bound(?bag_vbo),concat('<a href="',str(?bag_vbo), '">', 'Kadaster gegevens</a>:</h3>'), 'Kadaster gegevens:</h3>'),
      '<ul>',
      '<li><b>postcode:<b> ', if(bound(?postcode), str(?postcode), '?'), '</li>',
      '<li><b>oppervlakte:<b> ', if(bound(?opp), str(?opp), '?'), 'm2</li>',
      '<li><b>bouwjaar:<b> ', if(bound(?bouwjaar), str(?bouwjaar), '?'), '</li>',
      '<li><b>woz-klasse:<b> ', if(bound(?woz), str(?woz), '?'), '</li>',
      '</ul>',
      if(bound(?streetview), concat('<p><a href="', str(?streetview), '">street view</a></p>'), '')
    )
    as ?shapeLabel
  )
  ?transactie schema:buyer ?koper .
  ?koper vcard:hasAddress ?kopersadres ;
         foaf:name | skos:prefLabel ?kopersnaam .
  ?kopersadres
    vcard:street-address ?adreslabel_k ;
    naa:hasParcelCentroid/gsp:asWKT ?shape_k ;
                         vcard:locality ?woonplaats_k .
  optional {
    ?transactie schema:seller ?verkoper .
    ?verkoper vcard:hasAddress ?verkopersadres ;
              foaf:name ?verkopersnaam .
    ?verkopersadres
      vcard:street-address ?adreslabel_v ;
      naa:hasParcelCentroid/gsp:asWKT ?shape_v .
    optional {
      ?verkopersadres vcard:locality ?woonplaats_v .
    }
  }
  bind(
    concat(
      '<p>', ?verkopersnaam,'</p>',
      '<p>', ?adreslabel_v, ' ', (if (bound(?woonplaats_v), ?woonplaats_v, '?')), '</p>'
    )
    as ?shape_vLabel
  )
  bind(
    concat(
      '<p>', ?kopersnaam,'</p>',
      '<p>', ?adreslabel_k, ' ', (if (bound(?woonplaats_k), ?woonplaats_k, '?')), '</p>'
    )
    as ?shape_kLabel
  )
  bind("grey" as ?shapeColor)
  bind("green" as ?shape_vColor)
  bind("purple" as ?shape_kColor)
  bind(
    if(
      bound(?shape_v),
      concat(
        'LINESTRING(',
        strafter(strbefore(str(?shape_v), ')'), 'POINT('), ',',
        strafter(strbefore(str(?shape), ')'), 'POINT('), ')'
      ),
      '')
    as ?linestring_v
  )
  bind('green' as ?linestring_vColor)
  bind(
    if(
      bound(?shape_k),
      concat(
        'LINESTRING(',
        strafter(strbefore(str(?shape), ')'), 'POINT('), ',',
        strafter(strbefore(str(?shape_k), ')'), 'POINT('), ')'
      ),
      '')
    as ?linestring_k
  )
  bind('purple' as ?linestring_kColor)
}
