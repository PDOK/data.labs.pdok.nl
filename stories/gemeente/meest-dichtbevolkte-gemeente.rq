prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix begrip: <http://bag.basisregistraties.overheid.nl/id/begrip/>
prefix direct: <http://www.wikidata.org/prop/direct/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix geof: <http://www.opengis.net/def/function/geosparql/>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix uom: <http://www.opengis.net/def/uom/OGC/1.0/>
select ?wkt ?wktLabel {
  {
    select * {
      service <https://query.wikidata.org/sparql> {
        ?municipalityWiki direct:P981 ?bagId ;
                          direct:P1082 ?population .
        optional {
          ?municipalityWiki direct:P41 ?municipalityFlag
        }
      }
      ?municipality geo:hasGeometry/geo:asWKT ?wkt ;
                    bag:naamWoonplaats ?name ;
                    bag:identificatiecode ?bagId ;
                    bag:status begrip:WoonplaatsAangewezen ;
      filter not exists {
        ?municipality rdfs:isDefinedBy [
          bag:eindGeldigheid ?eindGeldigheid
        ]
      }
    }
  }
  bind(geof:envelope(?wkt) as ?rectangle)
  bind("^POLYGON\\s\\(\\(([0-9]*\\.[0-9]*)\\s([0-9]*\\.[0-9]*)\\,\\s([0-9]*\\.[0-9]*)\\s([0-9]*\\.[0-9]*)\\,\\s([0-9]*\\.[0-9]*)\\s([0-9]*\\.[0-9]*)\\,\\s([0-9]*\\.[0-9]*)\\s([0-9]*\\.[0-9]*).*\\)\\)$" as ?regex)
  bind(replace(str(?rectangle),?regex,"$1") as ?x1) bind(replace(str(?rectangle),?regex,"$2") as ?y1) bind(replace(str(?rectangle),?regex,"$5") as ?x2) bind(replace(str(?rectangle),?regex,"$4") as ?y2)
  bind(geof:distance(strdt(concat("Point (",str(?x1)," ",str(?y1),")"),geo:wktLiteral), strdt(concat("Point (",str(?x2)," ",str(?y1),")"),geo:wktLiteral), uom:metre) * 1e-3 as ?x)
  bind(geof:distance(strdt(concat("Point (",str(?x1)," ",str(?y1),")"),geo:wktLiteral), strdt(concat("Point (",str(?x1)," ",str(?y2),")"),geo:wktLiteral), uom:metre) * 1e-3 as ?y)
  bind(?x * ?y as ?area)
  bind (?population / ?area as ?density)
  bind (concat('<h3><a href="',str(?municipality),'">',str(?name),'</a></h3><p>Population: ',str(?population),'<br>Area: ',str(xsd:integer(?area)),' kmÂ²<br>Density: ',str(xsd:integer(?density)),' ðŸ‘¤/kmÂ²',if(bound(?municipalityFlag),concat('<img src="',str(?municipalityFlag),'">'),''),'</p>') as ?wktLabel)
}
order by desc(?density)
limit 25
