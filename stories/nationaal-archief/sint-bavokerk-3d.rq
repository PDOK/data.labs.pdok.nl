prefix bag: <http://bag.basisregistraties.overheid.nl/def/bag#>
prefix dce: <http://purl.org/dc/elements/1.1/>
prefix geo: <http://www.opengis.net/ont/geosparql#>
prefix pand: <http://bag.basisregistraties.overheid.nl/bag/id/pand/>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix schema: <http://schema.org/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
select ?shape ?shapeHeight ?shapeLabel ?shapeName {
  bind(40 as ?shapeHeight)
  {
    select
      (sample(?title) as ?title)
      (group_concat(?widget;separator='<hr>') as ?widgets)
    {
      {
        select
          ?image
          (strdt(?dateStr,xsd:date) as ?date)
          ?description
          (group_concat(distinct ?format;separator=', ') as ?format)
          ?pand
          (group_concat(distinct ?subject;separator=', ') as ?subject)
          ?title
          (sample(?url) as ?url)
        {
          values ?image {
            <http://proxy.handle.net/10648/a8902fa8-d0b4-102d-bcf8-003048976d84>
            <http://proxy.handle.net/10648/a89063ce-d0b4-102d-bcf8-003048976d84>
            <http://proxy.handle.net/10648/add8fd8c-d0b4-102d-bcf8-003048976d84>
          }
          ?image dce:date ?dateStr;
                 dce:description ?description;
                 dce:format ?format;
                 dce:subject ?subject;
                 dce:title ?title;
                 schema:contentLocation ?location;
                 schema:thumbnail ?url.
        }
        group by ?image ?dateStr ?description ?pand ?title
        order by desc(?date)
      }
      bind(concat(
        '<h5>',str(?description),'</h5>',
        '<figure>',
          '<a href="',str(?url),'" target="_blank">',
            '<img src="',str(?url),'" width="250px">',
          '</a>',
          '<figcaption>',
            '<dl>',
              '<dt>Datum</dt><dd>',year(?date),'-',month(?date),'-',day(?date),'</dd>',
              if(?format='','',concat('<dt>Formaten</dt><dd>',str(?format),'</dd>')),
              if(?subject='','',concat('<dt>Onderwerpen</dt><dd>',str(?subject),'</dd>')),
            '</dl>',
          '</figcaption>',
        '</figure>') as ?widget)
    }
    group by ?pand
  }
  bind(pand:0392100000065734 as ?pand)
  service <https://data.pdok.nl/sparql> {
    ?pand geo:hasGeometry/geo:asWKT ?shape;
          bag:identificatiecode ?pandId.
    ?verblijfsobject bag:hoofdadres ?nummeraanduiding;
                     bag:pandrelatering ?pand.
    ?openbareRuimte bag:naamOpenbareRuimte ?openbareRuimteNaam;
                    bag:bijbehorendeWoonplaats ?woonplaats.
    ?woonplaats rdfs:label ?woonplaatsNaam.
    ?nummeraanduiding bag:bijbehorendeOpenbareRuimte ?openbareRuimte;
                      bag:huisnummer ?huisnummer;
                      bag:postcode ?zipcode.
  }
  bind(concat('⛪ ',str(?openbareRuimteNaam),' ',str(?huisnummer),', ',str(?woonplaatsNaam)) as ?shapeName)
  bind(concat(
    '<center><h1>⛪ <a href="',str(?pand),'">',str(?openbareRuimteNaam),' ',str(?huisnummer),', ',str(?woonplaatsNaam),'</a></h1></center>',
    '<h2>Links uit PDOK hub</h2>',
    '<ul>',
      '<li><b>BAG</b>: <a href="',str(?pand),'">',str(?pandId),'</a></li>',
      '<li><b>RCE</b>: <a href="https://cultureelerfgoed.nl/monumenten/19264">19264</a></li>',
      '<li><b>Wikidata</b>: <a href="http://www.wikidata.org/entity/Q1545193">Q1545193</a></li>',
    '</ul>',
    '<h2>Afbeeldingen van Nationaal Archief</h2>',
    ?widgets
  ) as ?shapeLabel)
}
limit 1
